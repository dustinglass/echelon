beings = <whitespace>* name  (<whitespace>+ splitters <whitespace>+ name)* <whitespace>*
name   = token (<whitespace>+ token)* ["."*]

(*TODO: What to do about missing spaces. How smart can this parser actually get?*)
<token>   = simple | special

(*TODO: why does formers have to included here? It feels odd, perhaps the two negative lookups in simple and then splitters cancel out and formers get lost somehow*) 
<simple>  = !(special | splitters | formers | initials) #"[a-z0-9&!'/]+" ["."]

<special> = &(special-helper whitespace) special-helper
<special-helper> = and | corporates | domain | initials | north-america | number | outlier | saint | usa

and = "&" | "and" 

(*Canocilaization of org names*)
corporates = llc | pllc | llp | lp | lpa | corporation | limited | company | international | association | foreign 
association = "association" | "assn." | "associations" | "association's" | "associations'"
national = "national" | "nat'l"
international = "international"
llc  = "llc" | "lc" | "lcc" | "llc." | "l.l.c" | "l.l.c."
pllc = "pllc" | "p.l.l.c" | "p.l.l.c."
llp  = "llp" | "llp." | "lllp" | "llpp"
lp =   "lp" | "lp." | "l.p."
lpa = "lpa" | "l.p.a." | "l.p.a"
corporation = "corps" | "corporations" | "corporation" | "corp" | "corp." | "incorporated" | "inc" | "inc." | "inc ." | "in.c"
limited = "ltd" | "ltd." | "ltd.."
company = "company" | "companies" | "co." | "co"
foreign = "ltda." 

(*Dealing with edges cases around domains & initials)
initials = !(usa | north-america | splitters | corporates) initial+ [#"[a-z]"]
initial = #"[a-z]" "."
domain = [<"www.">] #'[a-z0-9]+' (<"."> ("com" | "org" | "us" | "net" | "il" | "tv" | "cl"))+

(*More shorthands *)
north-america = "north america" | &("n.a." whitespace) "n.a." | "north american"
number = &(number-helper whitespace) number-helper
<number-helper> = <["no." | "#"]> [<whitespace>] some-digits
<some-digits> = (digit | two-digits | three-digits) {"," three-digits} {digit} {"." digit*}
<two-digits> = digit digit
<three-digits> = digit digit digit 
<digit> = #"[0-9]"
<outlier> = "e.on" | "lch.clearnet"
saint = "st." | "saint" | "saints" | "st." simple
usa = &("u.s." whitespace) "u.s." | &("u.s.a" whitespace) "u.s.a" | &("u.s.a." whitespace) "u.s.a."


(*Extraction for also known as, formely known as, and on behalf of relationships*)
splitters = fka  | aka | obo
fka = "fka" | "f.k.a." | "f/k/a/" |  "f/k/a" | simple-fka  | complex-fka | "f.k.a"
<simple-fka>  = !complex-fka formers
<complex-fka> = formers <whitespace> fka-verbs [<whitespace> "as"]
<formers> = "formerly" | "formelry" | "formarly" | "frmly" | "frly" | "previously" | "prev."
<fka-verbs> = "registered" | "filed" | "reported" | "known" | "know" | "field" | "reporting" | "filing"  | "knows"

aka = "a/k/a" | "a.k.a." | "also known as"

obo = "on behalf of"

whitespace = ' ' | ',' | '-' | '(' | ')' | ':' | #'$' | '\"' | '/' | '*' | '=' | '>' | '+' | '[' | ']' | '_' | '$' | '\n' | '%'  | '\\' | '@' | ';' | '|' | '?' | ':'
